ScriptName ChkDiffyoneo
# 210202 K. Yonekura, Use yolov5 to check diffractin patterns

echo --------------------- ChkDiffyoneo ---------------------------

progdir = C:\ProgramData\yoneoLocr
watchinfile = InputImage.txt 

#ReportNavItem

echo Checking xtal by yoneoLocr
iflag = 0
i = 1
T
SaveToOtherFile A JPG JPG $progdir\Diff_$navLabel_$i.jpg
RunInShell echo Diff_$navLabel_$i.jpg > $progdir\WatchDiff\$watchinfile
xtalsp = {0., 0}
Loop 10 j
      OpenTextFile 1 T 0 $progdir\Diff_$navLabel_$i.log
       if $reportedValue1 == 1
            ReadTextFile xtalsp $progdir\Diff_$navLabel_$i.log
            CloseTextFile 1
            RemoveFile $progdir\Diff_$navLabel_$i.log
            break
       endif
       Delay 1s
EndLoop
RemoveFile $progdir\Diff_$navLabel_$i.jpg
if $xtalsp[1] == 0
   echo Can't indentify this pattern
   iflag = 1
elseif $xtalsp[2] == 0
   echo Diffraction is good
elseif $xtalsp[2] == 1
   echo Diffraction is soso
elseif $xtalsp[2] == 2
   echo Diffraction is bad
elseif $xtalsp[2] == 3
   echo No xtal diffraction
elseif $xtalsp[2] == 4
   echo Diffraction likely comes from ice
endif
if $xtalsp[1] != 0
    if $xtalsp[2] <= $xtalquality
       iflag = 1
    endif
endif

if $iflag == 1
 SaveToOtherFile A JPG JPG Diff_$navLabel.jpg 
endif
